plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'info.picocli:picocli:4.6.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

version = '1.3'
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Main-Class': 'net.darmo_creations.ti83_compiler.Main'
    }
    archiveClassifier = 'fat'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

tasks.register('createZip', Zip) {
    dependsOn jar
    dependsOn fatJar
    archiveFileName = "TI-83_Compiler-${version}.zip"
    destinationDirectory = layout.buildDirectory.dir('libs')
    from layout.projectDirectory
    include 'LICENSE'
    from layout.buildDirectory.dir('libs')
    include "TI-83_Compiler-${version}-fat.jar"
}
